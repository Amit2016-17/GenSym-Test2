#!/bin/csh -f

# Handles updates to java files for a single package.
# If class file is missing then add the java file for
# compilation, else run the make file generated by
# jmake to check for out of date class files.
set module_info_file=$1/package.info
if ( ! { test -f $module_info_file }) exit
ls $1/*.java >& /dev/null
if (! $status) then
  foreach JAVAFILE ($1/*.java)
    set basename = $JAVAFILE:r
    egrep "exclude-from-compile.*[^A-Za-z0-9]$basename:t.java" $module_info_file >& /dev/null
    if ($status && ! { test -f $basename.class } ) then
      add-for-java-compile $JAVAFILE
    endif
  end
endif
ls $1/*.class >& /dev/null
if ($status) exit 0
make -s -f /tmp/__generic_java_makefile $1/*.class >& /dev/null
