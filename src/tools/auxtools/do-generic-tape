#!/bin/csh -f
# See the following file for documentation.
source auxtools/wtape-init.csh

# Do-generic-tape makes tapes on no-rewind devices (i.e. tapes) that accept the
# mt commands rewind and offline.  It is used by make-gensym-tape, and
# mentioned in media-map.dat, another file used by make-gensym-tape.

# -jra 12/1/92

set drive_name=ERROR
set drive_host=ERROR

switch ( $media )

  case 'quarter-inch-6150':
    set drive_name=/dev/nrst2
    set drive_host=pl
    breaksw

#  case 'quarter-inch-600':
#    set drive_name=/dev/nrst0
#    set drive_host=pl
#    breaksw

  case '4mm-dat':
    set drive_name=/dev/nrst0
    set drive_host=pl
    breaksw

  case '8mm-video':
    set drive_name=/dev/nrst3
    set drive_host=pl
    breaksw

  case 'tk50-tar':
    set drive_name=/dev/nrmt1h
    set drive_host=lincoln
    breaksw

endsw

if ($drive_name == ERROR) then
  echo "ERROR: Unable to find tape drive for this media type, $media, and machine type, $platform."
  exit 1
endif

if ($drive_host == ERROR) then
  echo "ERROR: Unable to find drive host for this media type, $media, and machine type, $platform."
  exit 1
endif

set this_host = `hostname`
if ($this_host == $drive_host) then
  echo "    tape drive:    $drive_host (local):${drive_name}"
  switch ($command)
    case "write":
      cd $release_directory
      tar -chlf $drive_name *
      set result = $status
      breaksw
    case "rewind" :
      mt -f $drive_name rewind
      set result = $status
      breaksw
    case "online":
      set result = 0
      breaksw
    case "offline" :
      mt -f $drive_name offline
      set result = $status
      breaksw
    default :
      echo "ERROR: unknown command $command"
      set result = 1
      breaksw
  endsw
else
  echo "    tape drive:    ${drive_host}:${drive_name}"
  switch ($command)
    case "write":
      cd $release_directory
      tar -chlfb - 20 * | rsh $drive_host dd of=$drive_name obs=20b
      set result = $status
      breaksw
    case "rewind" :
      rsh $drive_host mt -f $drive_name rewind
      set result = $status
      breaksw
    case "online" :
      set result = 0
      breaksw
    case "offline" :
      rsh $drive_host mt -f $drive_name offline
      set result = $status
      breaksw
    default :
      echo "ERROR: unknown command $command"
      set result = 1
      breaksw
  endsw
endif

# Insert verification code here!

if ($result) then
  echo "ERROR: Failed to execute this command\!"
  echo "ERROR: DO NOT DISTRIBUTE this tape\!"
else
  echo "LOG: $command successful for ${drive_host}:${drive_name}."
  if ($command == offline) then
    echo "LOG: Please remove and WRITE PROTECT the tape."
  endif
endif

exit $result
