#!/bin/csh

#     Generate Corbalink for this platform

#     This script won't work if SRCBOX is not set or corba is missing
# ------------------------------------------------------------------
    if ("$SRCBOX" == "") then
        echo "** " This script requires a definition for SRCBOX
        exit -1
    else if (! -d $SRCBOX/corba) then
        echo "** " corba has not been checked out into $SRCBOX
        exit -2
    endif
    set md=$SRCBOX/corba/cpp


# See whether the required make file exists
# -----------------------------------------
    if ("$MACHINE" == "linux") then
        set mfn=corbalink-linux.mak
    else
        set mfn=corbalink-unix.mak
    endif
    if (! -f $md/$mfn) then
        echo "** " There is no make file to create Corbalink for $machine.
        exit -3
    endif

# cd to the work directory.
# -------------------------
    if (! -d $SRCBOX/dst/$machine/corbalink/opt) then
        echo "** " Destination directory \($SRCBOX/dst/$machine/corbalink/opt\) does not exist.
        exit -4
    endif
    cd $SRCBOX/dst/$machine/corbalink/opt

# If the bridge already exists and the f option is chosen, delete it.
# -------------------------------------------------------------------
    if ( $#argv > 1) then
        echo "*** Too many parameters"
        goto usage
    else if ( $#argv == 1 ) then
        if ( $argv[1] == 'f' || $argv[1] == 'F' ) then
            if ( -f g2-corba ) rm g2-corba
            make clean -f $md/$mfn
        else if ( $argv[1] == 'c' || $argv[1] == 'C' ) then
	    make clean -f $md/$mfn
            exit 0
        else
            echo "*** Parameter error"
            goto usage
        endif
    endif

#   Build the bridge
#   ----------------
    make `$md/cldefs` -f $md/$mfn
    exit 0

# Report how to use this script
# -----------------------------
usage:
    echo "Generate the Corbalink for this platform"
    echo "Usage: bcl [f | c]   where" 
    echo "          f means to force the rebuild;"
    echo "          c means remove the object files but don't build Corbalink"
    exit 1
