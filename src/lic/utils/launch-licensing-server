#!/bin/csh -f
onintr cleanup

#### Introduction

### This script is responsible for firing up the licensing server.

### - It knows how to decrypt the executable.
### - It knows how to push it into the background immune from the
###   death of the controling terminal.
### - It knows how to user logger to capture it's output.
### - It knows how to check if one is already running.

#### Kill Switch?
if ( $1 == -kill ) then
  set pinfo = ( `ps -auxgww | grep '/tmp/[l]icensing-server'` )
  if ( $#pinfo ) then
    echo "LOG `date` Killing running server"
    kill -9 $pinfo[2]
  endif
endif

#### Is one already running.
echo "LOG `date` Check against launching two licensing servers"
/gensym/bin/licensing-client -u guest -p anonymous noop > /tmp/$$
if( { grep -s 'Have a nice day.' /tmp/$$ } ) then
  echo "LOG `date` There is already a healthy licensing server."
  exit 1
endif


#### Check against race condition of two attempts to fix the problem.
echo "LOG `date` Check against multiple launch attempts"
set count = `ps -alxgww | grep '[l]aunch-licensing-server' | wc -l`
if ( $count > 1 ) then
  echo "LOG `date` More than launch one attempt in progress"
  exit 1
endif

#### Check if temp file already exists?
if ( -e /tmp/licensing-server ) then
  echo "LOG `date` temp. file already exists? /tmp/licensing-server."
  exit 1
endif


#### OK launch up a fresh server.

echo "LOG `date` Launching a fresh server, this takes a while..."

### The password to the decrypt is the union of the password
### given on the command line, information in /var/tmp/.lspw
### and finally the eithernet board address of the machine the
### licensing server must run upon.

set myaddr = `/usr/etc/ifconfig -au | sed -n 2p | awk '{print $2}'`
set key = "WaTeR$1`hostid``cat /var/tmp/.lspw`"
des -d -f -k $key /bt/siteok/lic/bin/ls.crypt /tmp/licensing-server
if ( $status ) goto cleanup
chmod u+x /tmp/licensing-server
yes | /tmp/licensing-server |& /bt/siteok/lic/utils/logger /bt/siteok/logs/licensing-server.log 1000 5 &



echo "LOG `date` Process launched, give it a little while to get going."
sleep 120

cleanup:
echo "LOG `date` All done."
rm -rf /tmp/licensing-server


