#!/bin/csh

#     Generate the specified database bridge for this platform

#     This script won't work if SRCBOX is not set or g2db is missing
# ------------------------------------------------------------------
    if ("$SRCBOX" == "") then
        echo "** " This script requires a definition for SRCBOX
        exit -1
    else if (! -d $SRCBOX/g2db) then
        echo "** " g2db has not been checked out into $SRCBOX
        exit -2
    endif
    set md=$SRCBOX/g2db/mak

#     Validate the code specifying which bridge is desired
# --------------------------------------------------------
    if ($#argv != 1 && $#argv != 2) then
        echo "** " Wrong number of arguments
        goto usage
    endif

    # We are going to construct directory names, makefile names,
    # environment file names, and program names.   To do this,
    # we use the following variables:
    #
    #   ln  - long name
    #   sn  - short name
    #   sfm - make file suffix
    #   sfe - suffix of program name
    #   mfx - make file suffix (needed for sybase make files)
    #   enm - environment name (ora or syb)
    #
    #   machine is an environment variable that is set on all Gensym's
    #   Unix machines to specify its operating system.
    #
    #   The makefile name (mfn) is g2-$sn$sfm$mfx-$machine.mak
    #                    Examples: g2-ora81-hp9000s700.mak
    #                              g2-sybase-11-sparcsol.mak
    #
    #   The environment file name is $enm<ver>-$machine.bat.
    #   (<ver> is determined by sed).
    #                      Examples: ora805-linux.bat
    #                                syb1110-alphaosf.bat
    #
    #   The bridge name is g2-$sn$sfe.
    #            Examples: g2-ora92
    #                      g2-sybase
    #
    #   It will be constructed in the directory $SRCBOX/dst/$machine/g2$ln/opt,
    #   e.g., $SRCBOX/dst/linux/g2oracle/opt or $SRCBOX/dst/rs6000/g2sybase/opt.

    if ($argv[1] == '7' || $argv[1] == '80' || $argv[1] == '81' || \
                           $argv[1] == '90' || $argv[1] == '92' || \
                           $argv[1] == '100' ) then
        set ln="oracle"
        set sn="ora"
        set sfm=$argv[1]
        set sfe=$argv[1]
        set mfx=""
        set enm="ora"
        if ($argv[1] == '100' ) then
            set sfe="10"
        endif
    else if ($argv[1] == '11'  || $argv[1] == '12' || \
             $argv[1] == '125' || $argv[1] == '1251' ) then
        set ln="sybase"
        set sn="syb"
        set sfm=$argv[1]
        set sfe=$argv[1]
        set mfx=""
        set enm="syb"
    else
        echo "** " Invalid bridge code
        goto usage
    endif


# See whether the required make file exists
# -----------------------------------------
    set mfn=g2-$sn$sfm$mfx-$machine.mak
    if (! -f $md/$mfn) then
    echo "*** $mfn ***"
    echo $md/$mfn
        echo "** " There is no make file to create g2-$sn$sfe for $machine.
        exit 2
    endif

# cd to the work directory.
# -------------------------
    if (! -d $SRCBOX/dst/$machine/g2$ln/opt) then
        echo "** " Destination directory \($SRCBOX/dst/$machine/g2$ln/opt\) does not exist.
        exit -3
    endif
    cd $SRCBOX/dst/$machine/g2$ln/opt

# Set the environment for building this bridge
# --------------------------------------------

# The following instruction constructs the name of the script that prepares the
# environment for building this bridge.  In particular, the sed command massages
# the XDBDIR line in the make file to extract the name of the final directory.
# For example, if we are build an Oracle bridge for rs6000 and the XDBDIR line
# says XDBDIR=/gensym/spare/bridges/rs6000/dbs/oracle/734 then the sed command
# will extract "734" and efn (environment file name) will be set to 
# ora734-rs6000.bat

set efn=${enm}`grep ^XDBDIR $md/$mfn|sed -e 's%^.*/\([^/]*\)$%\1%'`-$machine.bat
    if (! -r $SRCBOX/g2db/env/$efn) then
        echo "** "Script to initialize build environment \($SRCBOX/g2db/env/$efn\)
        echo "   is missing or not readable"
        exit -4
    endif
    source $SRCBOX/g2db/env/$efn

# If the bridge already exists and the f option is chosen, delete it.
# -------------------------------------------------------------------
    if ( $#argv == 2) then
        if ( $argv[2] == 'f' || $argv[2] == 'F' ) then
            if ( -f g2-$sn$sfe ) rm g2-$sn$sfe
            make clean -f $md/$mfn
        else
            if ( $argv[2] == 'c' || $argv[2] == 'C' ) then
                make gdb_client_ora.c -f $md/$mfn
                exit 0
            endif
        endif
    endif

#   Build the bridge and delete the intermediate files
#   --------------------------------------------------
    make `$md/gdefs` -f $md/$mfn
    make clean -f $md/$mfn
    exit 0

# Report how to use this script
# -----------------------------
usage:
    echo "Generate the database bridge specified by <code> for this platform"
    echo " "
    echo "         Usage: bdb <code> [f|c]"
    echo " "
    echo "  where the code specifies which bridge to build:"
    echo " "
    echo "           (7 - g2-ora7   80 - g2-ora80  81 - g2-ora81"
    echo "           100- g2-ora10  90 - g2-ora90  92 - g2-ora92"
    echo "           11 - g2-syb11  12 - g2-syb12  125- g2-syb125"
    echo "                         1251- g2-syb1251)"
    echo " "
    echo "       the optional f means to force the rebuild."
    echo "       the optional c means to just make the file gdb_client_ora.c"
    echo " "
    exit 1
